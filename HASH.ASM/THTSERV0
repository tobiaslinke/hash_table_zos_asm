*********************************************************************** 
* DESCRIPTION:                                                        * 
* ___________________________________________________________________ * 
* HASHTABLE SERVICE ROUTINE. PERFORMS DIV SAVE TO COMMIT ____________ * 
* ALL CHANGES._______________________________________________________ * 
* ___________________________________________________________________ * 
* THIS SERVICE MUST BE USED BY SPACE SWITCHING CALLERS SINCE_________ * 
* THE DIV SERVICE CAN'T BE CALLED IN A CROSS MEMORY ENVIRONMENT._____ * 
* THE SSWITCH PC ROUTINE WILL POST THE SERVERECB TO REQUEST_A________ * 
* SAVE OF THE HASHTABLE._____________________________________________ * 
* ___________________________________________________________________ * 
* ___________________________________________________________________ * 
* ENTRY: R1: @TAFWORK________________________________________________ * 
*********************************************************************** 
           TITLE    'THTSERV0 '                                         
           SYSSTATE ARCHLVL=2                                           
           ENTRY    THTSERV0                                            
THTSERV0   CSECT    ,                                                   
THTSERV0   AMODE    31                                                  
THTSERV0   RMODE    ANY                                                 
*********************************************************************** 
* REGISTER                                                              
*********************************************************************** 
R0         EQU      0                                                   
R1         EQU      1                                                   
R2         EQU      2                                                   
R3         EQU      3                                                   
R4         EQU      4                                                   
R5         EQU      5                                                   
R6         EQU      6                                                   
R7         EQU      7                                                   
R8         EQU      8                              @THTSERV0_BUFF       
R9         EQU      9                              @AUTOCNST            
R10        EQU      10                             @TAFWORK             
R11        EQU      11                             @WORKAREA            
R12        EQU      12                             @THTSERV0            
R13        EQU      13                             LINKAGE              
R14        EQU      14                             LINKAGE              
R15        EQU      15                             LINKAGE              
*********************************************************************** 
* ENTRY                                                                 
*********************************************************************** 
           USING    THTSERV0,R15                                        
           J        START                                               
           DC       C' PROGRAM NAME(THTSERV0)'                          
           DC       C' ASSEMBLY TIME(&SYSDATE &SYSTIME)'                
           DC       C' HASHTABLE SERVICE '                              
           DC       C' DESIGN: TOBIAS.LINKE@GKVI.DE'                    
START      DS       0H                                                  
           SAVE     (14,12)                        SAVE CALLER REGS     
           LR       R12,R15                  R12:  @THTSERV0            
           LR       R10,R1                   R10:  @TAFWORK             
           DROP     R15                            DROP TEMP ADDRESSING 
           USING    THTSERV0,R12                   ADDRESS THTSERV0     
           J        WORK1                                               
CONST      LOCTR    ,                                                   
CODE       LOCTR    ,                                                   
CODE       LOCTR    ,                                                   
*********************************************************************** 
* CREATE WORKAREA                                                       
*********************************************************************** 
WORK1      STORAGE  OBTAIN,LENGTH=WORKAREAL,LOC=31,BNDRY=DBLWD          
           LR       R11,R1                                              
           LR       R2,R1                                               
           LR       R4,R0                                               
           IILF     R3,WORKAREAL                                        
           SR       R1,R1                                               
           MVCL     R2,R0                                               
           USING    WORKAREA,R11                                        
           ST       R13,SAVEAREA+4                                      
           LA       R15,SAVEAREA                                        
           ST       R15,8(R13)                                          
           LR       R13,R15                  R13:  @SAVEAREA            
           ST       R4,WORKSIZE                                         
*********************************************************************** 
* MAIN                                                                  
*********************************************************************** 
           USING    TAFWORK,R10                    ADDRESS TAFWORK      
           L        R9,@AUTOCNST             R9:   @AUTOMAT CONSTANTS   
           USING    AUTOCNST,R9                    ADDRESS CONSTANTS    
*---------------------------------------------------------------------- 
*                                                                       
*          INITIALISATION                                               
*                                                                       
*---------------------------------------------------------------------- 
           SETMSG   MSG=SERV0_START                                     
           TAFWTO   TEXT=@MSG,$1=SERV0,MF=(E,WTO)                       
           ST       R12,RC                                              
           TAFCONVH SRC=RC,SRCL=4,TGT=RCCHAR,MF=(E,CONV)                
           SETMSG   MSG=TRACETCBADDR         @LOG: MY ADDRESS           
           TAFLOGIT TEXT=@MSG,MOD=SERV0,$1=RCCHAR,MF=(E,LOGIT)          
*---------------------------------------------------------------------- 
*          ADDRESS  INPUT BUFFER                                        
*---------------------------------------------------------------------- 
           L        R8,@THTSERV0_BUFF        R8:   @INPUT_BUFFER        
           USING    AUTOBUFF,R8                    ADDRESS INPUT BUFFER 
*---------------------------------------------------------------------- 
*          POST TAFMASTR                                                
*---------------------------------------------------------------------- 
           SETMSG   MSG=TCBINITC             LOG:  INITIALISATION COMP. 
           TAFLOGIT TEXT=@MSG,MOD=SERV0,MF=(E,LOGIT)                    
           SETMSG   MSG=POSTING              LOG:  POSTING MASTER       
           TAFLOGIT TEXT=@MSG,MOD=SERV0,$1=MASTR,MF=(E,LOGIT)           
           POST     SERV0_TO_MASTR,LINKAGE=SYSTEM                       
*---------------------------------------------------------------------- 
*          WAIT FOR CLIENTS                                             
*---------------------------------------------------------------------- 
           XC       SERVERECB,SERVERECB            RESET MY ECB         
           LA       R2,SERVERECB                                        
           ST       R2,@SERVERECB                                       
           LA       R2,MASTR_TO_SERV0                                   
           O        R2,LASTMARKER                                       
           ST       R2,@MASTR_TO_SERV0                                  
WAIT       DS       0H                                                  
           WAIT     ECBLIST=ECBLIST,LINKAGE=SYSTEM                      
           TM       MASTR_TO_SERV0,POSTED                               
           JZ       NEXT01                                              
*--------------------------------------------------                     
*          GOT POSTED BY MASTR TO SHUT DOWN                             
*--------------------------------------------------                     
           XC       MASTR_TO_SERV0,MASTR_TO_SERV0                       
           SETMSG   MSG=SHUTEVNT             LOG:  RECIEVED SHUTD SIGNL 
           TAFLOGIT TEXT=@MSG,MOD=SERV0,MF=(E,LOGIT)                    
           MODESET  MF=(E,SUP)                     SUPERVISOR MODE      
           DIV      SAVE,ID=HTTOKEN,SPAN=HTSIZE4K,MF=(E,DIV)            
           MODESET  MF=(E,PROB)                    BACK TO PROBLEM MODE 
           J        EXIT01                                              
NEXT01     TM       SERVERECB,POSTED                                    
           JZ       WAIT                                                
*---------------------------------------------------------------------- 
*          GOT POSTED BY CLIENT                                         
*          SAVE HASHTABLE                                               
*---------------------------------------------------------------------- 
           XC       SERVERECB,SERVERECB            RESET MY ECB         
           MODESET  MF=(E,SUP)                     SUPERVISOR MODE      
           DIV      SAVE,ID=HTTOKEN,SPAN=HTSIZE4K,MF=(E,DIV)            
           CIJNE    R15,0,ERROR01                  IF RC<>0 THEN ERROR  
           MODESET  MF=(E,PROB)                    BACK TO PROBLEM MODE 
           J        WAIT                           GO WAIT FOR CLIENTS  
*********************************************************************** 
* RETURN                                                                
*********************************************************************** 
EXIT01     DS       0H                                                  
           SETMSG   MSG=SHUTDOWN             LOG:  BEGIN SHUTDOWN       
           TAFLOGIT TEXT=@MSG,MOD=SERV0,MF=(E,LOGIT)                    
           L        R13,4(R13)               R13:  CALLERS SAVEAREA     
           L        R3,RC                    R3:   SAVE RC              
           L        R1,WORKSIZE                                         
           STORAGE  RELEASE,ADDR=(R11),LENGTH=(R1)                      
           LR       R15,R3                   R15:  RESTORE RC           
           RETURN   (14,12),RC=(15)                RETURN TO CALLER     
*********************************************************************** 
* ERRORS                                                                
*********************************************************************** 
ERROR01    DS       0H                                                  
           ST       R15,RC                         SAVE RC FOR CONV     
           ST       R0,RSN                         SAVE RSN FOR CONV    
           MODESET  MF=(E,PROB)                    BACK TO PROBLEM MODE 
           TAFCONVH SRC=RC,SRCL=4,TGT=RCCHAR,MF=(E,CONV)                
           TAFCONVH SRC=RSN,SRCL=4,TGT=RSNCHAR,MF=(E,CONV)              
           SETMSG   MSG=DIVSAVEFAIL          LOG:  DIV SAVE FAILED      
           TAFLOGIT TEXT=@MSG,MOD=SERV0,$1=RCCHAR,$2=RSNCHAR,          +
               MF=(E,LOGIT)                                             
           J        WAIT                           GO BACK TO WAIT      
*********************************************************************** 
* CONSTANTS                                                             
*********************************************************************** 
CONST      LOCTR    ,                                                   
@TAFWTO    DC       V(TAFWTO)                                           
LASTMARKER DC       X'80000000'                                         
SUP        MODESET  MODE=SUP,KEY=ZERO,MF=L         SUP PARMS LIST       
PROB       MODESET  MODE=PROB,KEY=NZERO,MF=L       PROBLEM PARMS LIST   
CODE       LOCTR    ,                                                   
*********************************************************************** 
* WORKAREA                                                              
*********************************************************************** 
WORKAREA   DSECT    ,                              MY WORKAREA          
SAVEAREA   DS       18F                            MY SAVEAREA          
WORKSIZE   DS       F                                                   
@MSG       DS       A                              PTR TO MESSAGE       
RC         DS       F                              RETURN CODE          
RSN        DS       F                              REASON CODE          
ECBLIST    DS       0F                                                  
@SERVERECB      DS  A                                                   
@MASTR_TO_SERV0 DS  A                                                   
DIV        DIV      SAVE,MF=L                      DIV PARMS LIST       
RCCHAR     DS       CL8                            RC EBCDIC            
RSNCHAR    DS       CL8                            RSN EBCDIC           
CONV       TAFCONVH MF=L                           TAFCONVH PARMS LIST  
LOGIT      TAFLOGIT MF=L                           TAFLOGIT PARMS LIST  
WTO        TAFWTO   MF=L                           TAFWTO   PARMS LIST  
WORKAREAL  EQU      *-WORKAREA                     LEN OF MY WORKAREA   
*********************************************************************** 
* DSECTS                                                                
*********************************************************************** 
           TAFWORK  ,                              TAF SHARED WORKAREA  
           TAFSVBLK ,                                                   
           TAFRECS  ,                                                   
           IHAPSA   ,                                                   
           END      THTSERV0                                            
