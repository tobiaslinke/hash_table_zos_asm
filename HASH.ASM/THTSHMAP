***********************************************************************
*  Typ: Subprogram                                                    *
*                                                                     *
* The Pointer to a Return Area is saved in THTCOMM Area at @BUCMAP    *
*                                                                     *
*  the caller is responsible for releasing the storage after they are *
*  done with it.                                                      *
*                                                                     *
*  Entry: R1: A(THTCOMM)                                              *
*                                                                     *
*  Exit:  R15: RC                                                     *
*         R1 : A(THTCOMM)                                             *
*         R0 : RSN                                                    *
*              0 --> bucket map created                               *
*              1 --> no HTCB found                                    *
***********************************************************************
         TITLE 'THTSHMAP create a Bucket Usage Map'
         SYSSTATE ARCHLVL=2
THTSHMAP RSECT
THTSHMAP AMODE 31
THTSHMAP RMODE ANY
*______________________________________________________________________ 
*
* Register                                                              
*______________________________________________________________________ 
R0       EQU    0                  Parameter
R1       EQU    1                  A(THTCOMM)                           
R2       EQU    2                  A(BUCKET)                            
R3       EQU    3                                                       
R4       EQU    4                  A(THTCOMM)                           
R5       EQU    5                                                       
R6       EQU    6                                                       
R7       EQU    7                  number of buckets                    
R8       EQU    8                  bucket length                        
R9       EQU    9                                                       
R10      EQU    10                 A(HTCB)
R11      EQU    11                 A(WORKAREA)                          
R12      EQU    12                 THTSELEC Basisregister               
R13      EQU    13                                                      
R14      EQU    14                                                      
R15      EQU    15                                                      
*______________________________________________________________________ 
*
* THTSHMAP entry                                                        
*______________________________________________________________________ 
         BAKR R14,0
         LR   R12,R15            THTSHMAP Basisregister
         LR   R4,R1              @THTCOMM to R4
         USING THTSHMAP,R12      Addressierung Konstanten+Hauptprogramm
         J     MAIN00
CONST    LOCTR ,
LITERAL  LOCTR ,
CODE     LOCTR ,
ERROR    LOCTR ,
*______________________________________________________________________
*
* THTSHMAP main
*______________________________________________________________________
*
CODE     LOCTR ,
MAIN00   DS    0H
         STORAGE OBTAIN,LENGTH=WORKAREAL,LOC=(31),BNDRY=DBLWD
         LR    R11,R1
         USING WORKAREA,R11
         USING THTCOMM,R4
         L     R2,@MAPAREA                  R2: begin LDS
         LR    R10,R2                       R10: A(HTCB)
         USING THTHTCB,R10
         CLC   0(4,R2),=CL4'HTCB'           exists HTCB?
         JNE   ERROR001                     no, error
ERROR    LOCTR ,
ERROR001 DS    0H
         IILF  R0,1                         RSN=1
         IILF  R15,8                        RC=8
         PR    ,
CODE     LOCTR ,
*----------------------------------------------------------------------
* get number of buckets
*----------------------------------------------------------------------
         L     R7,THTBUCKC                  R7: number of buckets
*----------------------------------------------------------------------
* address first bucket
*----------------------------------------------------------------------
         L     R8,THTBSIZE                  R8: Bucket size
         DROP  R10
*        A     R2,CBLOCKSL                  R2 A(first bucket)
*        AHI   R2,THTHTCBL                   + length HTCB
*----------------------------------------------------------------------
* obtain storage for return area
*----------------------------------------------------------------------
         LR    R5,R7                        R5: number buckets
         AHI   R5,THTSHMAPRL                + length return area header
         STORAGE OBTAIN,LENGTH=(R5),LOC=(31),BNDRY=DBLWD
         USING THTSHMAPR,R1                 adress return area
         ST    R5,THTSHMAPL                 set length return area
         ST    R7,THTSHMAPN                 set length returned value
         DROP  R1
         ST    R1,@THTRETURN                store pointer in THTCOMM
         DROP  R4
         AHI   R1,THTSHMAPRL                move pointer around header
*----------------------------------------------------------------------
* test sequentially all buckets
*----------------------------------------------------------------------
testBUCK DS    0H
         USING THTBUCK,R2                   address the bucket
         TM    THTKEYL,B'11111111'          is the bucket in use ?
         JNZ   markUSE                      yes, mark it
         MVI   0(R1),C'.'                   bucket is free
         LA    R1,1(,R1)                    move pointer 1Byte
         AR    R2,R8                        next bucket
         JCT   R7,testBUCK                  go test next bucket
         J     EXIT00
markUSE  DS    0H
         MVI   0(R1),C'X'                   bucket is in use
         LA    R1,1(,R1)                    move pointer 1Byte
         AR    R2,R8                        next bucket
         JCT   R7,testBUCK                  go test next bucket
         DROP  R2
*----------------------------------------------------------------------
* everything worked fine, return to caller
*----------------------------------------------------------------------
EXIT00   DS    0H
         STORAGE RELEASE,LENGTH=WORKAREAL,ADDR=(R11)
         LR    R1,R4
         IILF  R0,0                         RSN=0
         IILF  R15,0                        RC=0
         PR    ,                            return to caller
LITERAL  LOCTR ,
         LTORG
*______________________________________________________________________
*
* Variablen
*______________________________________________________________________
WORKAREA  DSECT
SAVEAREA  DS  18F
WORKAREAL EQU *-WORKAREA
*______________________________________________________________________
*
* Macros
*______________________________________________________________________
          THTCOMM
          THTCBLKS
          END
