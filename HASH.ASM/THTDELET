***********************************************************************
* Description:                                                        *
* ________________________________________________________________    *
* Delete a key/value record from a Table._________________________    *
* ________________________________________________________________    *
* Entry:___R1: @THTCOMM___________________________________________    *
* ________________________________________________________________    *
* Return:__R15: RC________________________________________________    *
* _________R1:  @THTCOMM__________________________________________    *
***********************************************************************
           TITLE    'THTDELET DELETE RECORD FROM HASHTABLE'
           SYSSTATE ARCHLVL=2
THTDELET   RSECT    ,                              REANTRANT
THTDELET   AMODE    31
THTDELET   RMODE    ANY
*********************************************************************** 
* REGISTER                                                              
*********************************************************************** 
R0         EQU      0
R1         EQU      1                              @THTCOMM             
R2         EQU      2                              @TBCB                
R3         EQU      3                              @BUCKET              
R4         EQU      4                              BUCKET INDEX         
R5         EQU      5                              BUCKET OFFSET        
R6         EQU      6                                                   
R7         EQU      7                                                   
R8         EQU      8                                                   
R9         EQU      9                                                   
R10        EQU      10                             @HTCB
R11        EQU      11                                                  
R12        EQU      12                             @THTDELET            
R13        EQU      13                             @FIRST BUCKET        
R14        EQU      14                             @BEGIN BUCKETAREA    
R15        EQU      15                             @END BUCKET AREA     
*********************************************************************** 
* ENTRY                                                                 
*********************************************************************** 
           BAKR     R14,0                          SAVE CALLER REGS
           LR       R12,R15                  R12:  @THTDELET
           USING    THTDELET,R12                   ADDRESS THTDELET
***********************************************************************
* MAIN
***********************************************************************
           USING    THTCOMM,R1                     ADDRESS THTCOMM
           RISBG    R1,R1,64,X'00'+31,32     R1H:  SAVE @THTCOMM
           L        R2,@MAPAREA              R2:   @HTCB
*----------------------------------------------------------------------
*          TEST IF HTCB EXISITS
*----------------------------------------------------------------------
           LR       R10,R2                   R10:  @HTCB
           USING    THTHTCB,R10                    ADDRESS HTCB
           IILF     R15,THTEYE               R15:  'HTCB' EYE CATCHER
           L        R14,0(,R2)               R14:  EYE CATCHER
           CRJNE    R15,R14,ERROR01                IF NO HTCB, ERROR
*----------------------------------------------------------------------
*          TEST IF TABLE EXISTS
*----------------------------------------------------------------------
           LA       R2,THTHTCBL(,R2)         R2:   @VECTOR OF TBCB's
           USING    THTTBCB,R2                     ADDRESS TBCB
           ICM      R9,B'1111',THTTBLC       R9:   NUMBER OF TABLES
           JZ       ERROR02                        IF NO TBCB's, ERROR
*----------------------------------------------------------------------
*          SEARCH ALL TBCB FOR OUR TABLE
*----------------------------------------------------------------------
           LLC      R4,TBLNAMEL              R4:   LENGTH TABLE NAME
           IILF     R0,THTEYE2               R0:   'TBCB' EYE CATCHER
           L        R15,@TBLNAME             R15:  @TABLE NAME
SRCHTABLE  L        R14,0(,R2)               R14:  TBCB EYE CATCHER
           CRJNE    R0,R14,NEXTSLOT                IF NO TBCB,NEXT SLOT
           LLC      R5,THTTNAML              R5:   TBLNAME LEN OF TBCB
           CRJNE    R5,R4,NEXTTBCB                 IF NOT SAME LEN,NEXT
           AHI      R5,-1                    R5:   DECR. LENGTH FOR CLC
           EX       R5,TBLCLC                      FOUND OUR TBCB ?
           JE       CALCHASH                       YES, CONTINUE
NEXTTBCB   LA       R2,THTTBCBL(,R2)               NO, NEXT TBCB
           JCT      R9,SRCHTABLE                   CONTINUE WHILE TBCBs
           J        ERROR03                        TABLE NOT FOUND
NEXTSLOT   LA       R2,THTTBCBL(,R2)         R2:   @NEXT TBCB
           J        SRCHTABLE
*----------------------------------------------------------------------
*          CALC HASH AND BUCKET INDEX
*          WE USE THE HASH OF THE TABLE NAME AS
*          A SEED FOR THE HASH OF THE KEY
*          BECAUSE WE DON'T WANT GUARANTEED COLLISONS
*          ON THE SAME KEYS IN DIFFERENT TABLES
*
*          FIRST WE HASH USING A MODIFIED VERSION OF
*          GOOGLE'S MURMUR3 FUNCTION. IF WE GET A COLLISION
*          WE REHASH WITH THE DJB2 FUNCTION.
*          IF WE COLLIDE AGAIN WE USE LINEAR PROBING TO FIND
*          A FREE BUCKET.
*----------------------------------------------------------------------
CALCHASH   DS       0H
           SR       R11,R11                  R11:  RESET COLLISION FLAG
*----------------------------------------------------------------------
*          TEST IF WE HAVE USED BUCKETS IN THE TABLE
*----------------------------------------------------------------------
           L        R0,THTTNBUC              R0:   NUM BUCKETS IN TBL
           CIJE     R0,0,ERROR04                   IF 0 THEN ERROR
           L        R3,THTTHASH              R3:   HASH OF TABLE NAME
           ST       R3,TBLHASH                     SET IN THTCOMM
*----------------------------------------------------------------------
*          MURMUR3 HASH FUNCTION
*----------------------------------------------------------------------
           L        R15,@THTHASH2            R15:  @THTHASH2
           BALR     R14,R15                        CALC HASH FOR KEY
           J        COMPRESS                       CALC BUCKET INDEX
*----------------------------------------------------------------------
*          REHASH USING DJB2 FUNCTION
*----------------------------------------------------------------------
REHASH     DS       0H
           L        R15,@THTHASH3            R15:  @THTHASH3
           BALR     R14,R15                        CALC HASH FOR KEY
*----------------------------------------------------------------------
*          COMPRESS THE HASH TO A BUCKET INDEX
*          BY DIVIDING THE HASH WITH THE BUCKETS COUNT
*          AND USING THE REMINDER AS THE INDEX NUMBER
*          THE BUCKETS COUNT SHOULD BE A PRIME NUMBER
*----------------------------------------------------------------------
COMPRESS   DS       0H
           L        R5,KEYHASH               R5:   32BIT HASH KEY
           L        R6,THTBUCKC              R6    COUNT BUCKETS
           SR       R4,R4                    R4    BUCKET INDEX
           DLR      R4,R6                          HASH / COUNT BUCKETS
*----------------------------------------------------------------------
*          CALC OFFSET OF BUCKET IN BYTES
*----------------------------------------------------------------------
           L        R8,THTBSIZE              R8:   BUCKET SIZE
           LR       R7,R4                    R7:   MY BUCKET INDEX
           SR       R6,R6                          ZERO OUT FOR MLR
           MLR      R6,R8                    R7:   OFFSET FROM 1. BUCK.
           L        R14,@THTBUCK             R14   RBA BUCKET AREA
           LA       R14,0(R14,R10)           R14   @BUCKET AREA
           LA       R3,0(R7,R14)             R3:   @MY BUCKET
           RISBG    R3,R3,64,X'00'+31,32     R3H:  SAVE @MY BUCKET
           L        R15,@THTBUCKE            R15:  RBA END BUCKET AREA
           LA       R15,0(R15,R10)           R15   @END BUCKET AREA
           ALR      R15,R8                   R15:  @END SEARCH AREA
           RISBG    R15,R15,64,X'00'+31,32   R15H: SAVE @LAST BUCKET
           RISBG    R14,R14,64,X'00'+31,32   R14H: SAVE @FIRST BUCKET
*----------------------------------------------------------------------
*          TEST IF BUCKET IS USED
*----------------------------------------------------------------------
TESTBUCK   DS       0H
           USING    THTBUCK,R3                     ADDRESS THE BUCKET
           TM       THTUFLAG,THTFREE               IS FREE?
           JZ       ERROR05                        YES, THEN ERROR
           TM       THTUFLAG,THTDEL                IF DELETED
           JO       TESTBUCK1                      SKIP THIS BUCKET
           L        R4,THTTID                R4:   TABLE ID WE LOOK FOR
           LLC      R9,KEYL                  R9:   LEN KEY WE LOOK FOR
           L        R5,THTBTID               R5:   TBLID OF THIS BUCKET
           CRJNE    R4,R5,TESTBUCK1                NOT OUR TBL,CONTINUE
           LLC      R13,THTKEYL              R13:  LEN KEY OF THIS BUC.
           CRJNE    R9,R13,TESTBUCK1               IF NOT EQUAL CONT.
           AHI      R13,-1                   R13:  DECR. LENGTH FOR CLC
           L        R5,@KEY                  R5:   @KEY
           EX       R13,KEYCLC                     IS THIS OUR KEY ?
           JE       DELETE                         YES, THEN DELETE IT
TESTBUCK1  CIJE     R11,1,PROBING                  REHASH COLLISON ?
*----------------------------------------------------------------------
*          AFTER 1. COLLISION WE REHASH AND INDEX TO NEW BUCKET
*          BUT FIRST WE TEST IF THIS BUCKET CONTAINS OUR KEY+TABLE ID
*----------------------------------------------------------------------
           IILF     R11,1                          INDICATE COLLISION
           J        REHASH                         REHASH
*----------------------------------------------------------------------
*          AFTER 2. HASH COLLISION
*          WE NOW USE LINEAR PROBING TO FIND OUR BUCKET
*          WE START AT THE INDEXED BUCKET AND SEARCH BACKWARDS
*          TOWARDS THE BEGINNING OF THE BUCKETS AREA
*          ONCE WE HIT THE FIRST BUCKET WE SET THE POINTER
*          TO THE LAST BUCKET AND SET THE
*          END POINTER TO THE INDEXED BUCKET WE STARTED AT
*          IF WE FIND AN EMPTY BUCKET BEFORE  THE BUCKET WE ARE
*          LOOKING FOR, WE RETURN AN ERROR
*----------------------------------------------------------------------
PROBING    L        R4,THTTID                R4:   TABLE ID WE LOOK FOR
           LLC      R9,KEYL                  R9:   LEN KEY WE LOOK FOR
NEXTBUCK   CRJE     R3,R14,ATBEGIN                 WE ARE AT THE BEGIN
           CRJE     R3,R15,ERROR05                 IF AT END ERROR
           SLR      R3,R8                    R3:   @PREVIOUS BUCKET
           TM       THTUFLAG,THTFREE               IS EMPTY?
           JZ       ERROR05                        YES, THEN ERROR
           TM       THTUFLAG,THTDEL                IF DELETED
           JO       NEXTBUCK                       SKIP THIS BUCKET
           L        R5,THTBTID               R5:   TBLID OF THIS BUCKET
           CRJNE    R4,R5,NEXTBUCK                 GO ON IF NOT OUR TBL
           LLC      R7,THTKEYL               R7:   LEN KEY OF THIS BUC.
           CRJNE    R9,R7,NEXTBUCK                 IF NOT EQUAL GO NEXT
           AHI      R7,-1                    R7:   DECR. LENGTH FOR CLC
           L        R5,@KEY                  R5:   @KEY
           EX       R7,KEYCLC                      IS THIS OUR KEY ?
           JE       DELETE                         YES, KEY IN TABLE
           J        NEXTBUCK                       NO, CONTINUE SEARCH
ATBEGIN    RISBG    R3,R15,32,X'00'+0,32     R3:   START FROM BOTTOM
           RISBG    R15,R3,32,X'00'+0,32     R15:  @MY BUCKET = NEW END
           J        NEXTBUCK                       CONTINUE SEARCH
*----------------------------------------------------------------------
*          DELETE KEY + VALUE FROM BUCKET
*          WE USE A SLIGHTLY MODIFIED VERSION OF
*          THE DELETE ALGORITHM DESICRIPED BY
*          DONALD E. KNUTH IN "THE ART OF COMPUTER PROGRAMMING"
*
*          WE START A LINEAR SEARCH FOR AN EMPTY BUCKET BEGINNING
*          AT THE BUCKET WE INDEXED TO AND SWAP EVERY BUCKET THAT
*          WAS INSERTED USING LINEAR PROBING AND ORIGINALLY INDEXED
*          TO A BUCKET WHICH RBA IS GEATER OR EQUAL TO THE ONE
*          IT IS SWAPPED TO. AFTER THE SEARCH WE MARK THE ORIGINAL
*          BUCKET OF THE LAST ONE WE SWAPPED AS EMPTY UNLESS
*          WE DON'T FIND ONE FOR SWAPPING. IN THIS CASE THE
*          ORIGINAL BUCKET WILL BE MARKED AS DELETED.
*          A DELETED BUCKET can ONLY BE REUSED BY THE
*          LINEAR PROBING ALGORITHM
*          INSTEAD OF REHASHING EVERY KEY IN THE SEARCH ORDER WE
*          STORED THE RBA OF THE ORIGINAL BUCKET IN THE BUCKET HEADER.
*----------------------------------------------------------------------
DELETE     DS       0H
*----------------------------------------------------------------------
*          UPDATE THE TABLE CHAINS FOR THE BUCKET WE WANT TO
*          DELETE.
*----------------------------------------------------------------------
           ICM      R1,B'1111',THTBUCKP      R1:   PREVIOUS BUCKET ?
           JZ       FIRSTBUCK1                     NO, IS FIRST IN TBL
           L        R14,THTBUCKN             R14:  RBA NEXT_BUCKET
           LA       R15,0(R1,R10)            R15:  @PREV_BUCKET
           DROP     R3                             DROP MY_BUCKET
           USING    THTBUCK,R15                    ADDRESS PREV_BUCKET
           ST       R14,THTBUCKN                   SET NEW NEXT_BUCKET
           DROP     R15                            DROP PREV_BUCKET
           USING    THTBUCK,R3                     ADDRESS MY_BUCKET
           J        NEXTBUCK1
FIRSTBUCK1 MVC      THTFBUCK,THTBUCKN              NEXT_BUCK=FIRST_BUCK
NEXTBUCK1  ICM      R1,B'1111',THTBUCKN      R1.   NEXT BUCKET ?
           JZ       LASTBUCK1                      NO, IS LAST IN TABLE
           L        R14,THTBUCKP             R14:  RBA PREV_BUCKET
           LA       R15,0(R1,R10)            R15:  @PREV_BUCKET
           DROP     R3                             DROP MY_BUCKET
           USING    THTBUCK,R15                    ADDRESS NEXT_BUCKET
           ST       R14,THTBUCKP                   SET NEW PREV_BUCKET
           DROP     R15                            DROP NEXT_BUCKET
           USING    THTBUCK,R3                     ADDRESS MY_BUCKET
           J        SEARCHO
LASTBUCK1  MVC      THTLBUCK,THTBUCKP              PREV_BUCK=LAST_BUCK
*----------------------------------------------------------------------
*          NOW WE SEARCH FOR BUCKETS TO SWAP INTO
*          THE LAST DELETED POSITION
*----------------------------------------------------------------------
SEARCHO    DS       0H                             FOUND ANOTHER BUCK ?
           SR       R11,R11                  R11:  SWAPPED INDICATOR
           LR       R13,R3                   R13:  SET LAST FOUND BUCK
           RISBG    R15,R15,32,X'00'+0,32    R15:  @END BUCKET AREA
           RISBG    R14,R14,32,X'00'+0,32    R15:  @BEGIN BUCKET AREA
           LR       R7,R3                    R7:   @MY_BUCKET
           SLR      R7,R10                   R7:   RBA MY_BUCKET
NEXTONE    CRJE     R3,R14,ATBEGIN2                WE ARE AT THE BEGIN
           CRJE     R3,R15,DELETE1                 IF AT END,CONTINUE
           SLR      R3,R8                    R3:   @PREVIOUS BUCKET
           TM       THTUFLAG,THTFREE               IS EMPTY?
           JZ       DELETE1                        YES, THEN FOUND LAST
           TM       THTUFLAG,THTDEL                IF DELETED
           JO       NEXTONE                        SKIP THIS BUCKET
           L        R5,THTBUCKO              R5:   RBA OF INDEXED BUCK
           CRJH     R5,R7,SWAPBUCKET               IF > OUR RBA, SWAP
           CRJE     R5,R7,SWAPBUCKET               IF > OUR RBA, SWAP
           J        NEXTONE                        NO, CONTINUE SEARCH
ATBEGIN2   RISBG    R3,R15,32,X'00'+0,32     R3:   START FROM BOTTOM
           RISBG    R15,R3,32,X'00'+0,32     R15:  @MY BUCKET = NEW END
           SR       R14,R14
           J        NEXTONE                        CONTINUE SEARCH
*----------------------------------------------------------------------
*          WE FOUND A BUCKET THAT ORIGINALLY INDEXED
*          TO AN RBA THAT IS GREATER THAN OURS.
*          NOW WE SWAP THAT BUCKET TO THE POSITION
*          OF THE LAST ONE WE FOUND, UPDATE THE TABLE CHAINS
*          AND CONTINUE THE SEARCH.
*          BUT FIRST WE MUST
*----------------------------------------------------------------------
SWAPBUCKET DS       0H
           IILF     R11,1                    R11:  INDICATE SWAPPED B.
*--------------------------------------------
*          MOVE THE BUCKET INTO THE POSITION
*          OF THE LAST DELETED ONE
*--------------------------------------------
           L        R1,THTBSIZE              R1:   BUCKET SIZE
           LR       R4,R3                    R4:   @MY_BUCKET
           LR       R5,R1                    R5:   BUCKET SIZE
           LR       R0,R13                   R0:   @LAST_DELETED_BUCKET
           MVCL     R0,R4                          MOVE INTO DELET. POS
           DROP     R3                             DROP MY_BUCKET
           USING    THTBUCK,R3                     ADDRESS OLD_BUCKET
           MVI      THTUFLAG,THTDEL                MARK AS DELETED
*--------------------------------------------
*          NOW WE MUST CORRECT THE CHAINS
*          OF THE MOVED BUCKET BECAUSE IT'S
*          RBA HAS  CHANGED.
*          IF THIS  BUCKET BELONGS TO ANOTHER
*          TABLE, WE MUST CHANGE ADDRESSING
*          TO THE NEW TBCB
*--------------------------------------------
           LR       R9,R13                   R9:   @THIS_BUCKET
           SLR      R9,R10                   R9:   RBA THIS_BUCKET
           L        R0,THTTID                R0:   CURRENT TABLE ID
           L        R1,THTBTID               R1:   MOVED BUCK TBL ID
           LR       R4,R2                    R4:   SAVE @TBCB
           CRJE     R0,R1,UPDATCHAIN               IF EQUAL, CONTINUE
           AHI      R1,-1
           MHI      R1,THTTBCBL              R1:   OFFSET IN TBCB AREA
           LA       R2,THTHTCBL(R1,R10)      R2:   @TBCB
UPDATCHAIN DS       0H
           ICM      R1,B'1111',THTBUCKP      R1:   PREVIOUS BUCKET ?
           JZ       FIRSTBUCK2                     NO, IS FIRST IN TBL
           LA       R1,0(R1,R10)             R1:   @PREV_BUCKET
           DROP     R3                             DROP MY_BUCKET
           USING    THTBUCK,R1                     ADDRESS PREV_BUCKET
           ST       R9,THTBUCKN                    SET NEW NEXT_BUCKET
           DROP     R1                             DROP PREV_BUCKET
           USING    THTBUCK,R3                     ADDRESS MY_BUCKET
           J        NEXTBUCK2
FIRSTBUCK2 ST       R9,THTFBUCK
NEXTBUCK2  ICM      R1,B'1111',THTBUCKN      R1.   NEXT BUCKET ?
           JZ       LASTBUCK2                      NO, IS LAST IN TABLE
           LA       R1,0(R1,R10)             R1:   @PREV_BUCKET
           DROP     R3                             DROP MY_BUCKET
           USING    THTBUCK,R1                     ADDRESS NEXT_BUCKET
           ST       R9,THTBUCKP                    SET NEW PREV_BUCKET
           DROP     R1                             DROP NEXT_BUCKET
           USING    THTBUCK,R3                     ADDRESS MY_BUCKET
           LR       R13,R3                   R13:  SAVE AS LAST DELETED
           LR       R2,R4                          RESTORE @TBCB
           J        NEXTONE                        CONTINUE SEARCH
LASTBUCK2  ST       R9,THTLBUCK
           LR       R13,R3                   R13:  SAVE AS LAST DELETED
           LR       R2,R4                          RESTORE @TBCB
           J        NEXTONE                        CONTINUE SEARCH
*----------------------------------------------------------------------
*          MARK THE LAST SWAPPED BUCKET
*          AS EMPTY OR IF NONE SWAPPED THE
*          ORIGINAL BUCKET AS DELETED
*----------------------------------------------------------------------
DELETE1    LR       R3,R13                   R3:   @LAST_DELETED_BUCKET
           CIJE     R11,1,DELETE2                  SWAPPED BUCKETS ?
           MVI      THTUFLAG,THTDEL                NO,MARK AS DELETED
           J        SETCOUNTER
DELETE2    XC       THTUFLAG,THTUFLAG              YES,MARK SWAP. EMPTY
*----------------------------------------------------------------------
*          SET HTCB BUCKET USAGE FIELDS
*----------------------------------------------------------------------
SETCOUNTER DS       0H
           ASI      THTBUCKU,-1                    DECR. USED BUCKETS
           ASI      THTBUCKF,1                     INCR. FREE BUCKETS
*----------------------------------------------------------------------
*          DECREMENT NUMBER RECORDS IN TABLE
*----------------------------------------------------------------------
           ASI      THTTNBUC,-1                    DECR. RECORDS COUNT
           SR       R15,R15                  R15:  RC=0
***********************************************************************
* RETURN
***********************************************************************
EXIT01     DS       0H
           RISBG    R1,R1,32,X'00'+0,32      R1:   RESTORE @THTCOMM
           PR       ,
***********************************************************************
* ERRORS
***********************************************************************
*----------------------------------------------------------------------
*          NO HTCB FOUND, HASHTABLE MUST BE  INIT
*----------------------------------------------------------------------
ERROR01    DS       0H
           IILF     R15,#NO_HTCB
           J        EXIT01
*----------------------------------------------------------------------
*          NO TABLES FOUND
*----------------------------------------------------------------------
ERROR02    DS       0H
           IILF     R15,#NO_TABLES
           J        EXIT01
*----------------------------------------------------------------------
*          TABLE NOT FOUND
*----------------------------------------------------------------------
ERROR03    DS       0H
           IILF     R15,#TABLE_NOT_FOUND
           J        EXIT01
*----------------------------------------------------------------------
*          TABLE IS EMPTY
*----------------------------------------------------------------------
ERROR04    DS       0H
           IILF     R15,#TABLE_IS_EMPTY
           J        EXIT01
*----------------------------------------------------------------------
*          KEY NOT  FOUND IN TABLE
*----------------------------------------------------------------------
ERROR05    DS       0H
           IILF     R15,#KEY_NOT_IN_TABLE
           J        EXIT01
***********************************************************************
* CONSTANTS
***********************************************************************
           DS       0F
@THTHASH2  DC       V(THTHASH2)
@THTHASH3  DC       V(THTHASH3)
TBLCLC     CLC      THTTNAME(0),0(R15)
           USING    THTBUCK,R3
KEYCLC     CLC      THTKEYL+1(0),0(R5)
           DROP     R3
           LTORG
***********************************************************************
* DSECTS
***********************************************************************
           THTCOMM  ,                              THT COMMAREA
           THTCBLKS ,                              THT CONTROL BLOCKS
           END      THTDELET
